apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

ext {
    versionMajor = 1
    versionMinor = 0
    versionPatch = 2
}

android {
    compileSdkVersion Versions.compile_sdk

    defaultConfig {
        applicationId "net.gearmaniacs.ftcscouting"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode versionMajor * 100 + versionMinor * 10 + versionPatch
        versionName "$versionMajor.$versionMinor.$versionPatch"
        resConfigs "en"
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        debug {
            manifestPlaceholders = [enableCrashlytics: false]
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
            aaptOptions.cruncherEnabled = false
            versionNameSuffix "-debug"
        }
        release {
            manifestPlaceholders = [enableCrashlytics: true]
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'app-rules.pro'
        }
    }
}

dependencies {
    implementation project(Modules.core)
    implementation project(Modules.login)
    implementation project(Modules.tournament)

    implementation Libs.firebase_crashlytics

    implementation Libs.material_intro
    implementation Libs.material_about
    implementation Libs.licenser

    testImplementation Libs.tests
}

apply plugin: 'com.google.gms.google-services'
