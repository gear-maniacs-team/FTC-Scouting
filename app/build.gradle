apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply plugin: 'dagger.hilt.android.plugin'

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

ext {
    versionMajor = 1
    versionMinor = 0
    versionPatch = 7
}

android {
    compileSdkVersion Versions.compile_sdk

    defaultConfig {
        applicationId "net.gearmaniacs.ftcscouting"
        minSdkVersion Versions.min_sdk
        targetSdkVersion Versions.target_sdk
        versionCode versionMajor * 100 + versionMinor * 10 + versionPatch
        versionName "$versionMajor.$versionMinor.$versionPatch"
        resConfigs "en"
    }

    buildFeatures {
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = [
                '-progressive',
        ]
    }

    buildTypes {
        debug {
            manifestPlaceholders = [disableFirebase: true, enableCrashlytics: false]
            ext.enableCrashlytics = false
            ext.alwaysUpdateBuildId = false
            aaptOptions.cruncherEnabled = false
        }
        release {
            manifestPlaceholders = [disableFirebase: false, enableCrashlytics: true]
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'app-rules.pro'
        }
    }
}

dependencies {
    implementation project(Modules.core)
    implementation project(Modules.login)
    implementation project(Modules.tournament)
    implementation project(Modules.database)

    implementation Libs.firebase_crashlytics

    implementation Libs.material_intro
    implementation Libs.material_about
    implementation Libs.licenser

    testImplementation Libs.tests

    implementation Libs.hilt
    kapt Libs.hilt_compiler
}
